# SPARQL query: Extract all ORDO mappings to SSSOM (with SKOS-conversion)
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX ORDO: <http://www.orpha.net/ORDO/Orphanet_>
PREFIX doid: <http://purl.obolibrary.org/obo/doid#>

SELECT ?subject_id ?subject_label ?predicate_id ?object_id ?status
WHERE {
    ?class skos:notation ?subject_id ;
        rdfs:label ?subject_label ;
        oboInOwl:hasDbXref ?object_id .
    FILTER(STRSTARTS(?subject_id, "ORPHA:"))

    OPTIONAL {
        ?xref_annot owl:annotatedSource ?class ;
            owl:annotatedProperty oboInOwl:hasDbXref ;
            owl:annotatedTarget ?object_id ;
            obo:ECO_0000218 ?xref_eco .

        BIND(
            REPLACE(
                str(?xref_eco), # convert to string to avoid possible language tag mismatches
                ".*(E|NTBT|BTNT|W|ND)(.|\\n)*",
                "$1"
            ) AS ?xref_type
        )

        VALUES (?xref_type ?predicate_id) {
            ("E" skos:exactMatch)
            ("BTNT" skos:narrowMatch)
            ("NTBT" skos:broadMatch)
            ("ND" doid:undefinedMatch)
            ("W" doid:notMatch)
        }
    }

    OPTIONAL {
        ?class rdfs:subClassOf* ?inactive .
        ?inactive rdfs:subClassOf ORDO:C041 ;
            rdfs:label ?inactive_label .
    }

    # clean up output: F where missing and no language tags
    BIND( if( BOUND( ?inactive_label ), ?inactive_label, "active") AS ?status)
}

# Return class & parent IDs and labels for all classes between a set of classes
# and their nearest common ancestor, inclusive
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX DOID: <http://purl.obolibrary.org/obo/DOID_>

SELECT ?id ?label ?parent_id ?parent_label
WHERE {
	{
		# find the nearest common ancestor
		SELECT ?ancestor_iri (COUNT(?mid) AS ?distance)
		WHERE {
			?ancestor_iri ^rdfs:subClassOf* ?mid .
			?mid ^rdfs:subClassOf* !<<set>>! .
			FILTER(!isBlank(?ancestor_iri))
		}
		GROUP BY ?ancestor_iri
		ORDER BY ?distance
		LIMIT 1
	}

	# get info for classes in path from set to ancestor
	VALUES ?set { !<<values_set>>! }

	?iri rdfs:subClassOf* ?ancestor_iri ;
		^rdfs:subClassOf* ?set ;
		oboInOwl:id ?id ;
		rdfs:label ?label ;
		rdfs:subClassOf ?parent_iri .
	
	?parent_iri oboInOwl:id ?parent_id ;
		rdfs:label ?parent_label .
}